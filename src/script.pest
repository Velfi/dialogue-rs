AllowedSymbols = {
    "!" | "@" | "#" | "$" | "%" | "^" | "&" | "*" | "(" | ")" |
    "{" | "}" | "[" | "]" | ";" | ":" | "'" | "\"" | "," | "." |
    "<" | ">" | "/" | "?" | "\\" | "`" | "~" | "-" | "_" | "=" | "+" 
}
Char = { (LETTER | NUMBER | PUNCTUATION | AllowedSymbols) }
Text = @{ (Char | SPACE_SEPARATOR)+ }

Marker = { "%" ~ MarkerName ~ "%" }
MarkerName = @{ (ASCII_ALPHA_UPPER | ASCII_DIGIT | "-")+ }

Comment = { SPACE_SEPARATOR* ~ "//" ~ SPACE_SEPARATOR* ~ Text ~ NEWLINE }

Prefix = @{ (!(" |") ~ (Char | SPACE_SEPARATOR))+ }
Command = { (Prefix ~ " ")? ~ "|" ~ CommandName ~ "|" ~ SPACE_SEPARATOR ~ Text }
CommandName = @{ (ASCII_ALPHA_UPPER | "-")+ }

Line = { (Marker | Command) ~ NEWLINE }

Block = {
	Comment? ~ 
    // The first line in the block
    PEEK_ALL ~ PUSH("    ") ~ Line ~
    // Subsequent lines in the block
    (Block+ | (PEEK_ALL ~ Line)*) ~
    // Remove the last layer of indentation from the stack when exiting the block
    DROP ~ NEWLINE*
}

Script = { SOI ~ (Block | Line | Comment)* ~ NEWLINE* }
